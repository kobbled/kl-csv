PROGRAM tst_csv_main
%NOLOCKGROUP

CONST
  PIPE_NAME = 'PIP:TESTCSV.DAT'
  CSV_NAME = 'FR:testcsv.csv'
  --set max write lines before reading
  --to TPDISLAY
  MAXLINES = 8
VAR
    csv_file: STRING[16]
    entries : INTEGER
    block_write : INTEGER
    waitime : INTEGER
    writedelay : INTEGER
    strt, done : BOOLEAN
    status : INTEGER

%include errors.klt
%include csv.klt

%include multitask.klh
%include display.klh
%include errors.klh
%include csv.klh

-- sensor csv file
%class csvpip('csvclass.klc','csvclass.klh')

BEGIN
    --set number of write operations
    entries = 20
    --set max wait time for read task
    waitime = 5000
    --time between write operations
    writedelay = 1000

    --set semaphore
    block_write = 1
    CLEAR_SEMA (block_write)
    --make writing immediately available
    POST_SEMA (block_write)

    --initialize pipe
    csvpip__constructor(PIPE_NAME, ',', POSITION_HEADER)
    csvpip__set_maxlines(MAXLINES)

    -- start reader
    strt = task__thread('tst_csv_read')
    IF NOT strt THEN
        karelError('could not start csv read task',ER_ABORT)
    ENDIF
    -- start writer
    strt = task__thread('tst_csv_writ')
    IF NOT strt THEN
        karelError('could not start csv write task',ER_ABORT)
    ENDIF


    -- keep parent program active until write task is complete
    REPEAT
        --check if done writing
        done = task__is_task_done('tst_csv_writ')
        DELAY 100
    UNTIL(done)

    done = task__abort('tst_csv_read')
    WRITE TPDISPLAY(CR,'csv read is done.')

    csvpip__destructor

END tst_csv_main
